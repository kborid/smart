apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "0.0.1"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //support
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'

    //logger
    api 'com.orhanobut:logger:2.2.0'

    //fastjson
    api 'com.alibaba:fastjson:1.2.56'

    //gson
    api 'com.google.code.gson:gson:2.8.5'

    //eventbus
    implementation 'org.greenrobot:eventbus:3.1.1'

    //ButterKnife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    //rxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.1'

    //dragger2
    implementation"com.google.dagger:dagger:2.14.1"
    annotationProcessor"com.google.dagger:dagger-compiler:2.14.1"

    implementation 'me.imid.swipebacklayout.lib:library:1.1.0'
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    //aar包增加源码
    archives androidSourcesJar
    //aar包增加注释
//    archives androidJavadocsJar
}

//指定编译的编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
tasks.withType(GroovyCompile) {
    groovyOptions.encoding = "MacRoman"
}

apply plugin: 'maven'

uploadArchives {
    repositories.mavenDeployer {
        repository(url: "file:///localMaven/Maven/")
        pom.artifactId = 'base'
        pom.groupId = 'com.kborid.library'
        pom.version = '1.0.0'
    }
}
