apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:27.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //logger
    api 'com.orhanobut:logger:2.2.0'

    //fastjson
    api 'com.alibaba:fastjson:1.2.56'

    //gson
    api 'com.google.code.gson:gson:2.8.5'
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    //aar包增加源码
    archives androidSourcesJar
    //aar包增加注释
//    archives androidJavadocsJar
}

//指定编译的编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
tasks.withType(GroovyCompile) {
    groovyOptions.encoding = "MacRoman"
}

apply plugin: 'maven'

uploadArchives {
    repositories.mavenDeployer {
//        repository(url: "http://repo.thunisoft.com/maven2/content/repositories/releases/") {
//            authentication(userName: "dev", password: "help")
//        }
        repository(url: "file:///localMaven/maven/")
        pom.artifactId = 'base'
        pom.groupId = 'com.kborid.library'
        pom.version = '1.0.0'
    }
}
